apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.yuyan.pinyin"
        minSdk 23
        targetSdk 34
        versionCode Integer.valueOf(versionCodeDate())
        versionName versionNameDate()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        lintOptions {
            abortOnError false
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk true
        }
    }

    signingConfigs {
        release {
            storeFile file("devdroid.jks")
            storePassword "devdroid110"
            keyAlias "devdroid"
            keyPassword "devdroid110"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    //多渠道打包
    flavorDimensions = ["default"]
    productFlavors {
        publishNonDefault true //唯一的不同
        official { dimension "default" }
    }
    productFlavors.configureEach {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    buildTypes {
        release {
            // 混淆
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix  ".release"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.release
        }

    }
    //    第三方库使用java1.8
    compileOptions {
        sourceCompatibility 17
        targetCompatibility 17
    }
    namespace 'com.yuyan'


    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if(abiName != null){
                outputFileName = "yuyanIme_${versionCodeDate()}_${abiName}.apk"
            } else {
                outputFileName = "yuyanIme_${versionCodeDate()}_full.apk"
            }

        }
    }
}

static def versionCodeDate() {
    return new Date().format("yyyyMMddHH", TimeZone.getTimeZone("GMT+8"))
}

static def versionNameDate() {
    return new Date().format("yyyyMMdd.HH", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':yuyansdk')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
}
